import groovy.json.JsonSlurper

def generatedSchemasDir = "$buildDir/generated_entity_schemas"
def domainTemplatesDir = "$buildDir/domain_templates"

sourceSets {
    main {
        resources {
            srcDir generatedSchemasDir
            srcDir domainTemplatesDir
            exclude 'schemas/custom', 'schemas/links'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'java-library'

dependencies {

    api project(":veo-core-usecase")

    implementation 'javax.validation:validation-api'
    implementation 'com.networknt:json-schema-validator'
    implementation 'com.github.victools:jsonschema-generator'
    implementation 'com.github.victools:jsonschema-module-jackson'
    implementation 'com.github.victools:jsonschema-module-javax-validation'
    implementation 'com.github.victools:jsonschema-module-swagger-2'
    implementation 'io.swagger.core.v3:swagger-annotations:2.1.11'

    compileOnly('org.projectlombok:lombok')
    compileOnly 'io.swagger.core.v3:swagger-core'

    testImplementation 'io.swagger.core.v3:swagger-core'
    testImplementation 'org.spockframework:spock-spring'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy:groovy-json'
}

task assembleDomainTemplates(type: JavaExec) {
    def targetDirectory = file("$domainTemplatesDir/domaintemplates/")
    def templateSnippetDir = rootProject.file('domaintemplates/')
    outputs.dir targetDirectory
    outputs.cacheIf { true }
    inputs.files compileJava.outputs
    inputs.files templateSnippetDir
    classpath compileJava.outputs.files
    classpath compileJava.classpath
    mainClass = 'org.veo.adapter.service.domaintemplate.DomainTemplateAssemblerMain'
    environment = [
        'domaintemplate.dir': "${templateSnippetDir}/dsgvo",
        'domaintemplate.abbreviation': "DS-GVO",
        'domaintemplate.authority': "SERNET",
        'domaintemplate.description': "The DS-GVO DomainTemplate.",
        'domaintemplate.id': "f8ed22b1-b277-56ec-a2ce-0dbd94e24824",
        'domaintemplate.name': "DS-GVO",
        'domaintemplate.out.file': "${targetDirectory}/dsgvo-example.json",
        'domaintemplate.revision': "latest",
        'domaintemplate.templateVersion': "1.0",
        'domaintemplate.catalogPrefixes': "tom",
        'tom.catalog.name': "DS-GVO-Controls",
        'tom.prefix': "TOM.",
        'domaintemplate.unit-dump-file': "${templateSnippetDir}/dsgvo/demo-unit/unit-dump.json"
    ]
}

plugins.withId('eclipse'){
    eclipse {
        synchronizationTasks assembleDomainTemplates
        autoBuildTasks assembleDomainTemplates
    }
}

processResources.dependsOn(assembleDomainTemplates)

task checkTranslations {
    def langFile = file('src/main/resources/lang/lang.json')
    inputs.file langFile
    def customAspectAndLinkFiles = rootProject.fileTree('domaintemplates/'){
        include '*/types/*/links/*.json'
        include '*/types/*/customAspects/*.json'
    }
    inputs.files customAspectAndLinkFiles
    def subTypeFiles = rootProject.fileTree(dir: 'domaintemplates/', include: '*/types/*/subTypes/*.json')
    inputs.files subTypeFiles

    doLast {
        def missingTranslations = false
        def langJson = new groovy.json.JsonSlurper().parse(langFile)
        def languagesToCheck = [
            'German': langJson.lang.de,
            'English': langJson.lang.en
        ]

        def handleTranslationKey = { String translationKey, location = null ->
            languagesToCheck.each {lang, json->
                def translation = json.remove(translationKey)
                if (translation == null) {
                    logger.warn("Missing $lang translation for $translationKey in $location")
                    missingTranslations = true
                }
            }
        }

        // Nameable base attributes
        handleTranslationKey('name', 'Nameable base attributes')
        handleTranslationKey('abbreviation', 'Nameable base attributes')
        handleTranslationKey('description', 'Nameable base attributes')

        customAspectAndLinkFiles.each { file->
            logger.info "Loading $file"
            def data = new groovy.json.JsonSlurper().parse(file)
            def isLinkSchema = file.parentFile.name == 'links'
            if (isLinkSchema) {
                // link IDs should be translated, custom aspect IDs should not
                handleTranslationKey(file.name.replace(".json", ""), "$file.name (custom link id)")
            }

            def properties = data.attributeSchemas
            def propertyNames = properties.keySet()
            logger.info "property names: $propertyNames"

            // custom aspect attribute ID / field in the editor
            properties.each {key, value->
                handleTranslationKey(key, "$file.name (custom aspect/link attribute id)")
                // custom aspect enum entry / value in drop-down
                value.enum?.each {
                    handleTranslationKey(it, "$file.name (custom aspect/link select value id)")
                }
                // custom aspect enum entry / value in multi-value drop-down
                value.items?.enum?.each {
                    handleTranslationKey(it, "$file.name (custom aspect/link select value id)")
                }
            }
        }

        subTypeFiles.each { File file ->
            def type = file.parentFile.parentFile.name.toLowerCase()
            def subType = file.name[0..-6]
            new JsonSlurper().parse(file).statuses.each{ status ->
                handleTranslationKey("${type}_${subType}_status_${status}", file.absolutePath)
            }
        }

        languagesToCheck.each {lang, json->
            json.each {
                logger.warn("Unused $lang translation $it")
            }
        }
        if (missingTranslations) {
            throw new GradleException("There are translations missing, see log output above.")
        }
    }
}

check.dependsOn checkTranslations