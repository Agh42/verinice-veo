
def generatedSchemasDir = "$buildDir/generated_entity_schemas"
def domainTemplatesDir = "$buildDir/domain_templates"

sourceSets {
    main {
        resources {
            srcDir generatedSchemasDir
            srcDir domainTemplatesDir
            exclude 'schemas/custom', 'schemas/links'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'java-library'

configurations {
    loggingImpl
}

dependencies {

    api project(":veo-core-usecase")
    api 'com.fasterxml.uuid:java-uuid-generator'

    implementation 'javax.validation:validation-api'
    implementation 'com.networknt:json-schema-validator'
    implementation 'com.github.victools:jsonschema-generator'
    implementation 'com.github.victools:jsonschema-module-jackson'
    implementation 'com.github.victools:jsonschema-module-javax-validation'
    implementation 'com.github.victools:jsonschema-module-swagger-2'
    implementation 'io.swagger.core.v3:swagger-annotations'
    implementation 'org.springdoc:springdoc-openapi-common'

    compileOnly('org.projectlombok:lombok')
    compileOnly 'io.swagger.core.v3:swagger-core'

    testImplementation 'io.swagger.core.v3:swagger-core'
    testImplementation 'org.spockframework:spock-spring'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.codehaus.groovy:groovy-json'

    loggingImpl 'org.slf4j:slf4j-simple'
}

task assembleDomainTemplates(type: JavaExec) {
    def targetDirectory = file("$domainTemplatesDir/domaintemplates/")
    def templateSnippetDir = rootProject.file('domaintemplates/')

    def config = [
        'domaintemplate.dir': "${templateSnippetDir}/dsgvo",
        'domaintemplate.abbreviation': "DS-GVO",
        'domaintemplate.authority': "SERNET",
        'domaintemplate.description': "The DS-GVO DomainTemplate.",
        'domaintemplate.name': "DS-GVO",
        'domaintemplate.out.file': "${targetDirectory}/dsgvo-example.json",
        'domaintemplate.revision': "latest",
        'domaintemplate.templateVersion': "1.8.0",
        'domaintemplate.catalogPrefixes': "tom",
        'tom.catalog.name': "DS-GVO",
        'tom.prefix': "TOM.",
        'domaintemplate.unit-dump-file': "${templateSnippetDir}/dsgvo/demo-unit/unit-dump.json"
    ]

    outputs.dir targetDirectory
    outputs.cacheIf { true }
    inputs.files compileJava.outputs
    inputs.files templateSnippetDir
    inputs.properties config
    classpath compileJava.outputs.files
    classpath compileJava.classpath
    classpath configurations.loggingImpl
    mainClass = 'org.veo.adapter.service.domaintemplate.DomainTemplateAssemblerMain'
    environment = config
}

plugins.withId('eclipse'){
    eclipse {
        synchronizationTasks assembleDomainTemplates
        autoBuildTasks assembleDomainTemplates
    }
}

processResources.dependsOn(assembleDomainTemplates)