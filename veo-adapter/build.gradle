def generatedSchemasDir = "$buildDir/generated_entity_schemas"

sourceSets {
    main {
        resources {
            srcDir generatedSchemasDir
            exclude 'schemas/custom', 'schemas/links'
        }
    }
}

apply plugin: 'groovy'
apply plugin: 'java-library'

configurations {
    generateSchemas
}

dependencies {

    api project(":veo-core-usecase")

    implementation 'javax.validation:validation-api'

    compileOnly('org.projectlombok:lombok')
    compileOnly 'io.swagger.core.v3:swagger-core'

    testImplementation 'io.swagger.core.v3:swagger-core'
    testImplementation 'org.spockframework:spock-spring'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'com.networknt:json-schema-validator'

    testRuntimeOnly 'com.h2database:h2'

    generateSchemas 'com.github.victools:jsonschema-generator'
    generateSchemas 'com.github.victools:jsonschema-module-jackson'
    generateSchemas 'com.github.victools:jsonschema-module-javax-validation'
    generateSchemas 'com.github.victools:jsonschema-module-swagger-2'
    generateSchemas 'io.swagger.core.v3:swagger-annotations:2.1.5'
}

task generateEntitySchemas(type: JavaExec) {
    def targetDirectory = file("$generatedSchemasDir/schemas")
    def schemaSnippetDir = file("src/main/resources/schemas")
    outputs.dir targetDirectory
    inputs.files compileJava.outputs
    inputs.files schemaSnippetDir
    classpath compileJava.outputs.files
    classpath compileJava.classpath
    classpath configurations.generateSchemas
    classpath file("${rootDir}/buildSrc/build/classes/java/main")
    main = 'org.veo.GenerateEntitySchema'
    args = [
        targetDirectory.absolutePath,
        schemaSnippetDir.absolutePath,
        'org.veo.adapter.presenter.api.dto.full',
        'Asset',
        'Control',
        'Document',
        'Incident',
        'Scenario',
        'Person',
        'Process',
        'Scope'
    ]
}

plugins.withId('eclipse'){
    eclipse {
        synchronizationTasks generateEntitySchemas
        autoBuildTasks generateEntitySchemas
    }
}

processResources.dependsOn(generateEntitySchemas)

task checkTranslations {
    def langFile = file('src/main/resources/lang/lang.json')
    inputs.file langFile
    def customSchemaFiles = fileTree(dir: 'src/main/resources/schemas/custom/', include: '**/*.json')
    inputs.files customSchemaFiles

    doLast {
        def langJson = new groovy.json.JsonSlurper().parse(langFile)
        def languagesToCheck = [
            'German': 'de',
            'English': 'en'
        ]

        def handleTranslationKey = {translationKey, json, location = null, isDummySchema = false->
            assert json != null, "No translations found for $location"
            languagesToCheck.each {lang, code->
                def translations = json[code]
                def translation = translations.remove(translationKey)
                if (translation == null) {
                    if (isDummySchema) {
                        logger.info("Missing $lang translation for $translationKey in dummy schema $location")
                    }else {
                        logger.warn("Missing $lang translation for $translationKey in $location")
                    }
                }
            }
        }

        def checkUnusedTransalations = { json, location ->
            languagesToCheck.each {lang, code->
                def translations = json[code]
                translations.each {
                    logger.warn("Unused $lang translation $it in $location")
                }
            }
        }

        // ModelObject base attributes
        handleTranslationKey('name', langJson.lang, 'ModelObject base attributes')
        handleTranslationKey('abbreviation', langJson.lang, 'ModelObject base attributes')
        handleTranslationKey('description', langJson.lang, 'ModelObject base attributes')

        customSchemaFiles.each { file->
            // FIXME remove dummy schema handling after VEO-363 and VEO-365 are done
            def isDummySchema = file.absolutePath.contains('/Asset/') ||  file.absolutePath.contains('/Control/')
            logger.info "Loading $file, isDummySchema=$isDummySchema"
            def data = new groovy.json.JsonSlurper().parse(file)
            def isLinkSchema = data.items != null
            if (isLinkSchema) {
                // relevant data is nested in link schemas
                data = data.items
                // link IDs should be translated, custom aspect IDs should not
                handleTranslationKey(data.properties.type.enum.first(), data.translations, "$file.name (custom link id)", isDummySchema)
            }

            def properties = data.properties.attributes.properties
            def propertyNames = properties.keySet()
            logger.info "property names: $propertyNames"

            // custom aspect attribute ID / field in the editor
            properties.each {key, value->
                handleTranslationKey(key, data.translations, "$file.name (custom aspect/link attribute id)", isDummySchema)
                // custom aspect enum entry / value in drop-down
                value.enum?.each {
                    handleTranslationKey(it,  data.translations,"$file.name (custom aspect/link select value id)", isDummySchema)
                }
            }
            checkUnusedTransalations(data.translations, file.name)
        }

        checkUnusedTransalations(langJson.lang, 'lang.json')
    }
}

check.dependsOn checkTranslations