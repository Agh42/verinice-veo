plugins {
    id 'org.springframework.boot' version '2.5.6' apply false
    id "com.github.spotbugs" version '4.7.1'
    id "com.diffplug.spotless" version "5.17.1" apply false
    id "io.freefair.lombok" version "6.2.0"
    id 'com.github.jk1.dependency-license-report' version '2.0'
}

def licenseFile3rdParty = 'LICENSE-3RD-PARTY.txt'

licenseReport {
    renderers = [
        new com.github.jk1.license.render.TextReportRenderer(licenseFile3rdParty),
    ]
    projects = [project]+ project.subprojects.findAll{it.name != 'veo-test'}
    filters = [
        new com.github.jk1.license.filter.LicenseBundleNormalizer()
    ]
}

def continuousIntegrationBuild = rootProject.hasProperty('ci')

allprojects  {
    group = 'org.veo'
    version = '0.8-SNAPSHOT'

    apply plugin: "com.diffplug.spotless"
    def currentUserName = "git config user.name".execute().text.trim() ?: "<name>"

    spotless {
        groovyGradle {
            greclipse()
            indentWithSpaces(4)
            trimTrailingWhitespace()
        }
        java {
            target 'src/**/*.java', 'buildSrc/**/*.java'
            eclipse().configFile(rootProject.file('misc/eclipse/java-format.xml'))
            importOrderFile(rootProject.file('misc/eclipse/java.importorder'))
            addStep org.veo.LicenseHeaderStep.create(currentUserName)
            addStep org.veo.NoWildcardImportsStep.create()
            removeUnusedImports()
            trimTrailingWhitespace()
            replaceRegex('Consecutive empty block comment lines', /( *\*\n){2,}/, '*\n')
            replaceRegex('Empty line at block comment end', / \*\n *\*\//, ' */')
            replaceRegex('Empty comment block', /\/\*+\s+\*\//, '')
            replaceRegex('Empty line after annotation', /(^ +@[a-zA-Z0-9]+\([^)]+\)\n)\n+/, '$1')
            replaceRegex('Empty line inside annotation', /(^ +@[a-zA-Z0-9]+\([^)]+\n)\n+/, '$1')
            replaceRegex('Default value for Schema#required()', /((?<=@Schema\([^)]{1,500}), required = false)|((?<=@Schema\()required = false(, )?)/, '')
        }
        groovy {
            target 'src/**/*.groovy', 'buildSrc/**/*.groovy'
            addStep org.veo.LicenseHeaderStep.create(currentUserName)
            addStep org.veo.NoWildcardImportsStep.create()
            greclipse()
            importOrderFile(rootProject.file('misc/eclipse/java.importorder'))
            indentWithSpaces(4)
            trimTrailingWhitespace()
            replaceRegex('Extra space around equals sign', /(  += )|( =  +)/, ' = ')
            replaceRegex('Not one space between right round and left curly bracket', /\) *\{/, ') {')
        }
        format 'xml', {
            target 'src/**/*.xml', 'misc/eclipse/java-format.xml'
            eclipseWtp('xml')
        }
        format 'git', {
            target '**/.gitignore'

            trimTrailingWhitespace()
            endWithNewline()
        }
        format 'markdown', {
            target '**/*.md'
            targetExclude '**/bin/**', '**/target/**'
            trimTrailingWhitespace()
            indentWithTabs()
            endWithNewline()
        }
        format 'json', {
            target '**/*.json'
            targetExclude '**/bin/**', '**/target/**'
            eclipseWtp('json').configFile rootProject.file('misc/eclipse/json-format.prefs')
            endWithNewline()
        }
        format 'Jenkinsfile', {
            target 'Jenkinsfile'
            indentWithSpaces(4)
            endWithNewline()
            trimTrailingWhitespace()
        }
    }
}

// work around for license report not being updated when the project's version number changes
// https://github.com/jk1/Gradle-License-Report/issues/223

generateLicenseReport.outputs.with {
    upToDateWhen { false }
    cacheIf { false }
}

task copy3rdPartyLicenseFile {
    generateLicenseReport.finalizedBy it
    doLast {
        file(licenseFile3rdParty).text = file("${generateLicenseReport.config.outputDir}/${licenseFile3rdParty}").text
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'io.freefair.lombok'

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

    def versions = [
        testcontainers: '1.16.0',
    ]

    dependencyManagement {
        dependencies {
            dependency 'org.postgresql:postgresql:42.3.0'
            dependency 'com.vladmihalcea:hibernate-types-52:2.12.1'
            dependency 'net._01001111:jlorem:1.3'
            dependency 'commons-cli:commons-cli:1.4'
            dependency 'javax:javaee-api:8.0.1'
            [
                'jjwt-api',
                'jjwt-impl',
                'jjwt-jackson'
            ].each { dependency "io.jsonwebtoken:$it:0.11.2" }
            [
                'spock-core',
                'spock-spring'
            ].each { dependency "org.spockframework:$it:2.0-groovy-3.0" }
            dependency 'org.apache.commons:commons-lang3:3.12.0'
            dependency 'org.codehaus.groovy:groovy:3.0.8'
            dependency 'javax.validation:validation-api:2.0.1.Final'
            // should be provided by spring boot dep. mgmt.:
            // dependency 'javax.transaction:javax.transaction-api:1.3'
            dependency 'org.springdoc:springdoc-openapi-ui:1.5.12'
            dependency 'io.swagger.core.v3:swagger-core:2.1.11'
            dependency 'com.github.javafaker:javafaker:1.0.2'
            [
                'jsonschema-generator',
                'jsonschema-module-jackson',
                'jsonschema-module-javax-validation',
                'jsonschema-module-swagger-2'
            ].each { dependency "com.github.victools:$it:4.20.0" }
            dependency 'net.ttddyy:datasource-proxy:1.7'
            dependency 'com.networknt:json-schema-validator:1.0.60'
            dependency "org.testcontainers:testcontainers:$versions.testcontainers"
            dependency "org.testcontainers:spock:$versions.testcontainers"
            dependency "org.testcontainers:postgresql:$versions.testcontainers"
            dependency "org.keycloak:keycloak-authz-client:11.0.3"
        }
    }

    sourceCompatibility = 11
    targetCompatibility = 11

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    repositories { mavenCentral() }

    tasks.withType(Test) {
        useJUnitPlatform()
        systemProperties project.properties.subMap([
            "http.proxyHost",
            "http.proxyPort",
            "https.proxyHost",
            "https.proxyPort"
        ])
    }

    pmd {
        toolVersion = '6.36.0'
        ruleSetConfig = rootProject.resources.text.fromFile('misc/pmd/rules.xml')
        ruleSets = []
        rulesMinimumPriority = 2
        consoleOutput = true
        sourceSets = project.sourceSets.matching {it.name != 'test'}
    }

    spotbugs.excludeFilter = rootProject.file('misc/spotbugs/exclude.xml')
    spotbugsTest.enabled = false
    // write human readable report on normal builds
    spotbugsMain.reports {
        // Jenkins needs xml reports to detect and display bugs.
        xml.enabled = continuousIntegrationBuild
        html.enabled = !continuousIntegrationBuild
    }

    // specific "continuous integration" settings needed by Jenkins
    if (continuousIntegrationBuild) {

        // The recordIssues Jenkins plugin reads from the console output, so we need to write the warnings
        // on every build. Otherwise Jenkins will assume the warnings have been fixed.
        tasks.withType(JavaCompile) {
            outputs.upToDateWhen { false }
        }

        tasks.withType(Test) {
            // Don't let failing tests fail the build, let the junit step in the Jenkins pipeline decide what to do
            ignoreFailures = true
            testLogging { exceptionFormat = 'full' }
        }

        // Update the test result file timestamps so Jenkins picks them up even when doing incremental builds
        task jenkinsTestFix {
            inputs.files test.outputs.files
            doLast {
                if (!sourceSets.test.allSource.empty) {
                    def timestamp = System.currentTimeMillis()
                    test.reports.junitXml.outputLocation.get().asFile.eachFile { it.lastModified = timestamp }
                }
            }
        }

        test {
            finalizedBy jenkinsTestFix
        }
    }

    dependencies {
        compileOnly 'org.slf4j:slf4j-api'
        runtimeOnly 'ch.qos.logback:logback-classic'
        spotbugsPlugins "jp.skypencil.findbugs.slf4j:bug-pattern:1.5.0@jar"
    }

    tasks.withType(JavaCompile) {
        configure(options){
            debugOptions.debugLevel = "source,lines,vars"
            // See https://docs.oracle.com/en/java/javase/11/tools/javac.html#GUID-AEEC9F07-CB49-4E96-8BC7-BCC2C7F725C9
            // for a detailed list of linting keys.
            compilerArgs.addAll([
                '-Xlint:all',
                '-Xlint:-serial'
            ])
        }
    }
}
